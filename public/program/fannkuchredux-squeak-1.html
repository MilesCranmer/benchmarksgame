<!DOCTYPE html>
<meta name="viewport" content="width=device-width,initial-scale=1">
<meta name="robots" content="noindex,follow,noarchive">

<title>fannkuch-redux Squeak Smalltalk program | Computer Language Benchmarks Game </title>
<style><!--
a{color:black;text-decoration:none}article{padding:0 0 2.9em}article,div,footer,header{margin:auto;width:92%}body{font:100% Droid Sans, Ubuntu, Verdana, sans-serif;margin:0;-webkit-text-size-adjust:100%}h3, h1, h2, li a{font-family:Ubuntu Mono,Consolas,Menlo,monospace}div,footer,header{max-width:31em}footer{padding:2.6em 0 0}h3{font-size:1.4em;font-weight:bold;margin:0;padding: .4em}h3, h3 a{color:white;background-color:#dd4814}h3 small{font-size:0.64em}h1,h2{margin:1.5em 0 0}h1{font-size:1.4em;font-weight:normal}h2{font-size:1.2em}li{list-style-type:none;vertical-align:top}li a{display:block;font-size:1.2em;margin: .5em .5em 0;padding: .5em .5em .3em}ul{clear:left;margin:-0.3em 0 1.5em;padding-left:0;text-align:center}p{color:#333;line-height:1.4;margin: .3em 0 0}p a, a span{border-bottom: .1em solid #333;padding-bottom: .1em}.com,.slc{color:#888}.kwa{color:#066}.kwb{color:#900}.kwc{color:#050}.kwa,.kwb,.kwc{font-weight:bold}.dstr,.str,.sym,.num{color:#930}pre{color:#222;font-size:1em;overflow-wrap:break-word;white-space:pre-wrap;word-wrap:break-word}@media only screen and (min-width: 60em){article,footer,header{font-size:1.25em}}
--></style>
<link rel="shortcut icon" href="./favicon.ico">
<header>
  <h3><a href="https://benchmarksgame-team.pages.debian.net/benchmarksgame/">The&nbsp;<small>Computer&nbsp;Language</small><br>Benchmarks&nbsp;Game</a></h3>
</header>
<article>
  <div>
    <h1>fannkuch-redux Squeak Smalltalk program</h1>
    <aside>
      <p><a href="../description/fannkuchredux.html#fannkuchredux">description</a>
    </aside>
  </div>
  <section>
    </div>
      <h2>source code</h2>
    </div>
    <pre>
&quot;* The Computer Language Benchmarks Game
   https://salsa.debian.org/benchmarksgame-team/benchmarksgame/
   contributed by Paolo Bonzini 
   modified by Isaac Gouy *&quot;!

Object subclass: #BenchmarksGame
   instanceVariableNames: &apos;&apos;
   classVariableNames: &apos;&apos;
   poolDictionaries: &apos;&apos;
   category: &apos;&apos;!

Object subclass: #PermGeneratorRedux
   instanceVariableNames: &apos;timesRotated perm atEnd&apos;
   classVariableNames: &apos;&apos;
   poolDictionaries: &apos;&apos;
   category: &apos;benchmarks game&apos;!


!PermGeneratorRedux class methodsFor: &apos;instance creation&apos;!

new: size
   ^self new
      initialize: size;
      yourself! !


!PermGeneratorRedux methodsFor: &apos;accessing&apos;!

atEnd
   ^atEnd!

maxPfannkuchenTo: output
   | max permutation checksum permCount flipsCount |
   max := 0.
   permCount := 0.
   checksum := 0.
   [self atEnd] whileFalse:
      [permutation := self next.
      permCount := permCount + 1.
      (permCount = 1048576) ifTrue: [permCount := 0].
      flipsCount := permutation pfannkuchen.
      checksum := permCount odd 
         ifTrue: [checksum+flipsCount] 
         ifFalse: [checksum-flipsCount].
      max := max max: flipsCount].
   output print: checksum; nl.
   ^max!

next
   | result |
   result := perm copy.
   self makeNext.
   ^result! !

!PermGeneratorRedux methodsFor: &apos;initialize-release&apos;!

initialize: size
   perm := (1 to: size) asArray.
   timesRotated := Array new: size withAll: 0.
   atEnd := false.!

makeNext
   | temp remainder |
   &quot;* Generate the next permutation. *&quot;
   2 to: perm size do: [ :r |
      &quot;* Rotate the first r items to the left. *&quot;
      temp := perm at: 1.
      1 to: r - 1 do: [ :i | perm at: i put: (perm at: i + 1) ].
      perm at: r put: temp.

      remainder := timesRotated at: r 
                                put: ((timesRotated at: r) + 1) \\ r.
      remainder = 0 ifFalse: [ ^self ].

      &quot;* After r rotations, the first r items 
         are in their original positions.
      Go on rotating the first r+1 items. *&quot;
   ].

   &quot;* We are past the final permutation. *&quot;
   atEnd := true! !


!BenchmarksGame class methodsFor: &apos;private&apos;!

fannkuchRedux: n to: output
   ^(PermGeneratorRedux new: n) maxPfannkuchenTo: output! !

!BenchmarksGame class methodsFor: &apos;initialize-release&apos;!

program
   | n f |
   n := (Smalltalk getSystemAttribute: 3) asInteger.
   f := self fannkuchRedux: n to: UnixProcess stdOut.
   UnixProcess stdOut
      nextPutAll: &apos;Pfannkuchen(&apos;, n printString, &apos;) = &apos;;
      print: f; nl.

   ^&apos;&apos;! !


!Array methodsFor: &apos;benchmarks game&apos;!

pfannkuchen
   | first complement a b k |
   k := 0.
   [ (first := self at: 1) == 1 ] whileFalse: [
      k := k + 1.
      complement := first + 1.
      1 to: first // 2 do: [ :i |
         a := self at: i.
         b := self at: complement - i.
         self at: i put: b.
         self at: complement - i put: a.
      ]
   ].
   ^k! !


!Stream methodsFor: &apos;benchmarks game&apos;!

nl
   self nextPut: Character lf! !
    </pre>
  </section>
  <section>
    <h2 id="log">notes, command-line, and program output</h2>
    <pre>
NOTES:
64-bit Ubuntu quad core
Squeak5.2-18229-64bit-201810190412-Linux


Thu, 21 Mar 2019 02:44:44 GMT

MAKE:
cp /opt/src/Squeak5.2-18229-64bit-201810190412-Linux/shared/Squeak5.2-18229-64bit.image fannkuchredux.squeak_run.image
cp /opt/src/Squeak5.2-18229-64bit-201810190412-Linux/shared/Squeak5.2-18229-64bit.changes fannkuchredux.squeak_run.changes
cp Include/squeak/OSProcessV4-3-3.sar .
cat Include/squeak/make.st
| prog cur |

&quot;load stdio package&quot;

(SARInstaller new) 
   directory: FileDirectory default; 
   fileName: 'OSProcessV4-3-3.sar'; 
   fileIn.


&quot;load program to be measured&quot;

prog := Smalltalk getSystemAttribute: 3.

(prog notNil) ifTrue: [
   FileStream fileIn: prog].


&quot;clean up&quot;

cur := Project current world.
cur allNonFlapRelatedSubmorphs do: [:m | m delete].

(SystemWindow windowsIn: cur 
      satisfying: [:w | w model canDiscardEdits])
   do: [:w | w delete].

Smalltalk garbageCollect.

SmalltalkImage current snapshot: true andQuit: true.


/opt/src/Squeak5.2-18229-64bit-201810190412-Linux/bin/squeak -headless fannkuchredux.squeak_run.image Include/squeak/make.st fannkuchredux.squeak 2&gt;/dev/null
cat Include/squeak/run.st
BenchmarksGame program!
SmalltalkImage current snapshot: false andQuit: true!



6.62s to complete and log all make actions

COMMAND LINE:
/opt/src/Squeak5.2-18229-64bit-201810190412-Linux/bin/squeak -headless fannkuchredux.squeak_run.image Include/squeak/run.st 12

PROGRAM OUTPUT:
3968050
Pfannkuchen(12) = 65

pthread_setschedparam failed: Operation not permitted
This VM uses a separate heartbeat thread to update its internal clock
and handle events.  For best operation, this thread should run at a
higher priority, however the VM was unable to change the priority.  The
effect is that heavily loaded systems may experience some latency
issues.  If this occurs, please create the appropriate configuration
file in /etc/security/limits.d/ as shown below:

cat &lt;&lt;END | sudo tee /etc/security/limits.d/squeak.conf
*      hard    rtprio  2
*      soft    rtprio  2
END

and report to the squeak mailing list whether this improves behaviour.

You will need to log out and log back in for the limits to take effect.
For more information please see
https://github.com/OpenSmalltalk/opensmalltalk-vm/releases/tag/r3732#linux
    </pre>
  </section>
</article>
<footer>
  <nav>
    <ul>
      <li><a href="../license.html"><span>3-Clause BSD License</span></a>
    </ul>
  </nav>
</footer>

