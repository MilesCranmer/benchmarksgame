<!DOCTYPE html>
<meta name="viewport" content="width=device-width,initial-scale=1">
<meta name="robots" content="noindex,follow,noarchive">

<title>mandelbrot Erlang&nbsp;#3 program | Computer Language Benchmarks Game </title>
<style><!--
a{color:black;text-decoration:none}article{padding:0 0 2.9em}article,div,footer,header{margin:auto;width:92%}body{font:100% Droid Sans, Ubuntu, Verdana, sans-serif;margin:0;-webkit-text-size-adjust:100%}h3, h1, h2, li a{font-family:Ubuntu Mono,Consolas,Menlo,monospace}div,footer,header{max-width:31em}footer{padding:2.6em 0 0}h3{font-size:1.4em;font-weight:bold;margin:0;padding: .4em}h3, h3 a{color:white;background-color:#77216f}h3 small{font-size:0.64em}h1,h2{margin:1.5em 0 0}h1{font-size:1.4em;font-weight:normal}h2{font-size:1.2em}li{list-style-type:none;vertical-align:top}li a{display:block;font-size:1.2em;margin: .5em .5em 0;padding: .5em .5em .3em}ul{clear:left;margin:-0.3em 0 1.5em;padding-left:0;text-align:center}p{color:#333;line-height:1.4;margin: .3em 0 0}p a, a span{border-bottom: .1em solid #333;padding-bottom: .1em}.com,.slc{color:#888}.kwa{color:#066}.kwb{color:#900}.kwc{color:#050}.kwa,.kwb,.kwc{font-weight:bold}.dstr,.str,.sym,.num{color:#930}pre{color:#222;font-size:1em;overflow-wrap:break-word;white-space:pre-wrap;word-wrap:break-word}@media only screen and (min-width: 60em){article,footer,header{font-size:1.25em}}
--></style>
<link rel="shortcut icon" href="../favicon.ico">
<header>
  <h3><a href="https://benchmarksgame-team.pages.debian.net/benchmarksgame/index.html">The&nbsp;<small>Computer&nbsp;Language</small><br>Benchmarks&nbsp;Game</a></h3>
</header>
<article>
  <div>
    <h1>mandelbrot Erlang&nbsp;#3 program</h1>
    <aside>
      <p><a href="../description/mandelbrot.html#mandelbrot">description</a>
    </aside>
  </div>
  <section>
    <div>
      <h2>source code</h2>
    </div>
    <pre>
% The Computer Language Benchmarks Game
% https://salsa.debian.org/benchmarksgame-team/benchmarksgame/
%% Contributed by Johan Karlsson based on Fredrik Svahn&apos;s mandelbrot program
%% Added usage of port_command by Johan Karlsson

-module(mandelbrot).
-export([main/1]).
-define(LIM_SQR, 4.0).
-define(ITER, 50).
-define(SR, -1.5).
-define(SI, -1).

main([Arg]) -&gt;
    N = list_to_integer(Arg),

    %% Start a printing process that prints using 
    %% port_command to decrease memory usage
    PrintProc = print_start(),
    print(PrintProc,[&quot;P4\n&quot;, Arg, &quot; &quot;, Arg, &quot;\n&quot;]),

    %% Spawn one process per row
    Row = fun(NextProc,Y)-&gt; row(PrintProc, NextProc, N-1, 0, ?SI+Y*2/N, N, 0, [], 7) end,
    spawn_proc_chain(PrintProc,Row, N).

%% A function that spawns a chain of processes.
spawn_proc_chain(PrintProc,Row, N) -&gt;
    Spawn = fun(PP,S,F,I,NN) -&gt; spawn(fun() -&gt; do_spawn_proc_chain(PP,S,F,I,NN) end) end,
    Spawn(PrintProc,Spawn,Row,first,N).

do_spawn_proc_chain(PrintProc,_,_,Max,Max) -&gt;
    receive done -&gt; ok end,
    %% Needed in order to let the print proccess finish printing.
    print_stop(PrintProc),
    halt(0);
do_spawn_proc_chain(PP,Spawn,Row,first,Max) -&gt;
    NextProc = Spawn(PP,Spawn,Row,1,Max),
    %% I&apos;m the first process in the chain. Inform my self that I can finish.
    self() ! done,
    %% Execute the row function
    Row(NextProc,0);
do_spawn_proc_chain(PP,Spawn,Row,N,Max) -&gt;
    NextProc = Spawn(PP,Spawn,Row,N+1,Max),
    Row(NextProc,N).


%% Iterate over a row, collect bits, bytes and finally print the row
row(PrintProc, NextProc,X,X, _, _, Bits, Bytes, C) -&gt;
    Char = case C of
               7 -&gt; lists:reverse(Bytes);
               C -&gt; lists:reverse([Bits bsl (C+1) | Bytes])
           end,
    %% Wait for the previous process to finish before printing
    receive _ -&gt; ok end,
    print(PrintProc, Char),
    NextProc ! done;

row(PP, NP,M,X, Y2, N, Bits, Bytes, 0) -&gt;
    row(PP, NP,M,X+1, Y2, N, 0, [Bits bsl 1 + m(?ITER, ?SR+(X+X)/N, Y2) | Bytes], 7);

row(PP, NP,M,X, Y2, N, Bits, Bytes, BitC) -&gt;
    row(PP, NP,M,X+1, Y2, N, Bits bsl 1 + m(?ITER, ?SR+(X+X)/N, Y2), Bytes, BitC-1).


%Mandelbrot algorithm
m(Iter, CR,CI) -&gt; m(Iter - 1, CR, CI, CR, CI).

m(Iter, R, I, CR, CI) -&gt;
    case R*R+I*I &gt; ?LIM_SQR of 
	false when Iter &gt; 0 -&gt; m(Iter-1, R*R-I*I+CR, 2*R*I+CI, CR, CI);
	false -&gt; 1;
	true -&gt; 0
    end.


%% Print stuff
print(PP,Char) -&gt;
    PP ! Char.

print_start() -&gt;
    spawn(fun() -&gt; do_print_start() end).

print_stop(PrintProc) -&gt;
    PrintProc ! {self(),stop},
    receive stopped -&gt; ok end.

do_print_start() -&gt;
    Fd = open_port({fd,0,1}, [out]),
    print_loop(Fd).

print_loop(Fd) -&gt;
    receive {Pid,stop} -&gt;
                Pid ! stopped;
            Char -&gt; 
                port_command(Fd, Char), print_loop(Fd)
     end.
    </pre>
  </section>
  <section>
    <div>
      <h2 id="log">notes, command-line, and program output</h2>
    </div>
    <pre>
NOTES:
64-bit Ubuntu quad core
Erlang/OTP 24 [erts-12.0] [source] [64-bit]
[smp:4:4] [ds:4:4:10] [async-threads:1] [jit]



Thu, 13 May 2021 01:15:33 GMT

MAKE:
mv mandelbrot.erlang-3.erlang mandelbrot.erl
/opt/src/otp_src_24.0/bin/erlc mandelbrot.erl

0.57s to complete and log all make actions

COMMAND LINE:
/opt/src/otp_src_24.0/bin/erl -smp enable -noshell -run  mandelbrot main 16000

(BINARY) PROGRAM OUTPUT NOT SHOWN
    </pre>
  </section>
</article>
<footer>
  <nav>
    <ul>
      <li><a href="../license.html"><span>3-Clause BSD License</span></a>
    </ul>
  </nav>
</footer>

