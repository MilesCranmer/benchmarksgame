<!DOCTYPE html>
<meta name="viewport" content="width=device-width,initial-scale=1">
<meta name="robots" content="noindex,follow,noarchive">

<title>n-body Squeak Smalltalk program | Computer Language Benchmarks Game </title>
<style><!--
a{color:black;text-decoration:none}article{padding:0 0 2.9em}article,div,footer,header{margin:auto;width:92%}body{font:100% Droid Sans, Ubuntu, Verdana, sans-serif;margin:0;-webkit-text-size-adjust:100%}h3, h1, h2, li a{font-family:Ubuntu Mono,Consolas,Menlo,monospace}div,footer,header{max-width:31em}footer{padding:2.6em 0 0}h3{font-size:1.4em;font-weight:bold;margin:0;padding: .4em}h3, h3 a{color:white;background-color:#dd4814}h3 small{font-size:0.64em}h1,h2{margin:1.5em 0 0}h1{font-size:1.4em;font-weight:normal}h2{font-size:1.2em}li{list-style-type:none;vertical-align:top}li a{display:block;font-size:1.2em;margin: .5em .5em 0;padding: .5em .5em .3em}ul{clear:left;margin:-0.3em 0 1.5em;padding-left:0;text-align:center}p{color:#333;line-height:1.4;margin: .3em 0 0}p a, a span{border-bottom: .1em solid #333;padding-bottom: .1em}.com,.slc{color:#888}.kwa{color:#066}.kwb{color:#900}.kwc{color:#050}.kwa,.kwb,.kwc{font-weight:bold}.dstr,.str,.sym,.num{color:#930}pre{color:#222;font-size:1em;overflow-wrap:break-word;white-space:pre-wrap;word-wrap:break-word}@media only screen and (min-width: 60em){article,footer,header{font-size:1.25em}}
--></style>
<link rel="shortcut icon" href="./favicon.ico">
<header>
  <h3><a href="https://benchmarksgame-team.pages.debian.net/benchmarksgame/">The&nbsp;<small>Computer&nbsp;Language</small><br>Benchmarks&nbsp;Game</a></h3>
</header>
<article>
  <div>
    <h1>n-body Squeak Smalltalk program</h1>
    <aside>
      <p><a href="../description/nbody.html#nbody">description</a>
    </aside>
  </div>
  <section>
    </div>
      <h2>source code</h2>
    </div>
    <pre>
&quot;* The Computer Language Benchmarks Game
    https://salsa.debian.org/benchmarksgame-team/benchmarksgame/
    contributed by Isaac Gouy *&quot;!

Object subclass: #NBodySystem
   instanceVariableNames: &apos;bodies&apos;
   classVariableNames: &apos;&apos;
   poolDictionaries: &apos;&apos;
   category: &apos;benchmarks game&apos;!

Object subclass: #Body
   instanceVariableNames: &apos;x y z vx vy vz mass&apos;
   classVariableNames: &apos;&apos;
   poolDictionaries: &apos;&apos;
   category: &apos;benchmarks game&apos;!

Object subclass: #BenchmarksGame
   instanceVariableNames: &apos;&apos;
   classVariableNames: &apos;&apos;
   poolDictionaries: &apos;&apos;
   category: &apos;&apos;!

!Body class methodsFor: &apos;constants&apos;!

daysPerYear
   ^365.24d0!

sun
   ^self new
      x: 0.0d0
      y: 0.0d0
      z: 0.0d0
      vx: 0.0d0
      vy: 0.0d0
      vz: 0.0d0
      mass: self solarMass!

uranus
   ^self new
      x: 1.28943695621391310d1
      y: -1.51111514016986312d1
      z: -2.23307578892655734d-1
      vx: 2.96460137564761618d-3 * self daysPerYear
      vy: 2.37847173959480950d-3 * self daysPerYear
      vz: -2.96589568540237556d-5 * self daysPerYear
      mass: 4.36624404335156298d-5 * self solarMass!

saturn
   ^self new
      x: 8.34336671824457987d0
      y: 4.12479856412430479d0
      z: -4.03523417114321381d-1
      vx: -2.76742510726862411d-3 * self daysPerYear
      vy: 4.99852801234917238d-3 * self daysPerYear
      vz: 2.30417297573763929d-5 * self daysPerYear
      mass: 2.85885980666130812d-4 * self solarMass!

solarMass
   ^4.0d0 * self pi * self pi!

pi
   ^3.141592653589793d0!

jupiter
   ^self new
      x: 4.84143144246472090d0
      y: -1.16032004402742839d0
      z: -1.03622044471123109d-1
      vx: 1.66007664274403694d-3 * self daysPerYear
      vy: 7.69901118419740425d-3 * self daysPerYear
      vz: -6.90460016972063023d-5 * self daysPerYear
      mass: 9.54791938424326609d-4 * self solarMass!

neptune
   ^self new
      x: 1.53796971148509165d1
      y: -2.59193146099879641d1
      z: 1.79258772950371181d-1
      vx: 2.68067772490389322d-3 * self daysPerYear
      vy: 1.62824170038242295d-3 * self daysPerYear
      vz: -9.51592254519715870d-5 * self daysPerYear
      mass: 5.15138902046611451d-5 * self solarMass! !


!Body methodsFor: &apos;accessing&apos;!

z
   ^z!

x
   ^x!

mass
   ^mass!

x: d1 y: d2 z: d3 vx: d4 vy: d5 vz: d6 mass: d7
   x := d1.
   y := d2. 
   z := d3. 
   vx := d4.
   vy := d5.
   vz := d6.
   mass := d7!

y
   ^y! !

!Body methodsFor: &apos;nbody&apos;!

offsetMomentum: anArray 
   | m |
   m := self class solarMass.
   vx := (anArray at: 1) negated / m.
   vy := (anArray at: 2) negated / m.
   vz := (anArray at: 3) negated / m!

decreaseVelocity: dx y: dy z: dz m: m
   vx := vx - (dx * m).
   vy := vy - (dy * m).
   vz := vz - (dz * m)!

positionAfter: dt
   x := x + (dt * vx).
   y := y + (dt * vy).
   z := z + (dt * vz)!

and: aBody velocityAfter: dt        
   | dx dy dz distance mag |
   dx := x - aBody x.
   dy := y - aBody y.
   dz := z - aBody z.
   
   distance := ((dx*dx) + (dy*dy) + (dz*dz)) sqrt.
   mag := dt / (distance * distance * distance).

   self decreaseVelocity: dx y: dy z: dz m: aBody mass * mag.   
   aBody increaseVelocity: dx y: dy z: dz m: mass * mag!

potentialEnergy: aBody
   | dx dy dz distance |
   dx := x - aBody x.
   dy := y - aBody y.
   dz := z - aBody z.

   distance := ((dx*dx) + (dy*dy) + (dz*dz)) sqrt.
   ^mass * aBody mass / distance!

addMomentumTo: anArray
   anArray at: 1 put: (anArray at: 1) + (vx * mass).
   anArray at: 2 put: (anArray at: 2) + (vy * mass).
   anArray at: 3 put: (anArray at: 3) + (vz * mass).
   ^anArray!

increaseVelocity: dx y: dy z: dz m: m
   vx := vx + (dx * m).
   vy := vy + (dy * m).
   vz := vz + (dz * m)!

kineticEnergy
   ^0.5d0 * mass * ((vx * vx) + (vy * vy) + (vz * vz))! !


!NBodySystem methodsFor: &apos;initialize-release&apos;!

initialize
   bodies := OrderedCollection new
      add: Body sun; add: Body jupiter; add: Body saturn;
      add: Body uranus; add: Body neptune; yourself.

   bodies first offsetMomentum:
      (bodies inject: (Array with: 0.0d0 with: 0.0d0 with: 0.0d0)
         into: [:m :each | each addMomentumTo: m])! !

!NBodySystem methodsFor: &apos;nbody&apos;!

after: dt
   1 to: bodies size do: [:i|
      i+1 to: bodies size do: [:j|                            
         (bodies at: i) and: (bodies at: j) velocityAfter: dt].
   ].   
   bodies do: [:each| each positionAfter: dt]!

energy
   | e |
   e := 0.0d0.
   1 to: bodies size do: [:i|       
      e := e + (bodies at: i) kineticEnergy.

      i+1 to: bodies size do: [:j| 
         e := e - ((bodies at: i) potentialEnergy: (bodies at: j))].
   ].
   ^e! !

!BenchmarksGame class methodsFor: &apos;initialize-release&apos;!

program
   | n bodies |
   n := (Smalltalk getSystemAttribute: 3) asInteger.

   bodies := NBodySystem new initialize.
   UnixProcess stdOut print: bodies energy digits: 9; nl.
   n timesRepeat: [bodies after: 0.01d0].
   UnixProcess stdOut print: bodies energy digits: 9; nl.

   ^&apos;&apos;! !

!Stream methodsFor: &apos;benchmarks game&apos;!

nl
   self nextPut: Character lf!

print: number digits: decimalPlaces
   | precision rounded |
   decimalPlaces &lt;= 0 ifTrue: [^ number rounded printString].
   precision := Utilities floatPrecisionForDecimalPlaces: decimalPlaces.
   rounded := number roundTo: precision.
   self nextPutAll: 
      ((rounded asScaledDecimal: decimalPlaces) printString copyUpTo: $s)! !


    </pre>
  </section>
  <section>
    <h2 id="log">notes, command-line, and program output</h2>
    <pre>
NOTES:
64-bit Ubuntu quad core
Squeak5.2-18229-64bit-201810190412-Linux


Thu, 21 Mar 2019 04:14:23 GMT

MAKE:
cp /opt/src/Squeak5.2-18229-64bit-201810190412-Linux/shared/Squeak5.2-18229-64bit.image nbody.squeak_run.image
cp /opt/src/Squeak5.2-18229-64bit-201810190412-Linux/shared/Squeak5.2-18229-64bit.changes nbody.squeak_run.changes
cp Include/squeak/OSProcessV4-3-3.sar .
cat Include/squeak/make.st
| prog cur |

&quot;load stdio package&quot;

(SARInstaller new) 
   directory: FileDirectory default; 
   fileName: 'OSProcessV4-3-3.sar'; 
   fileIn.


&quot;load program to be measured&quot;

prog := Smalltalk getSystemAttribute: 3.

(prog notNil) ifTrue: [
   FileStream fileIn: prog].


&quot;clean up&quot;

cur := Project current world.
cur allNonFlapRelatedSubmorphs do: [:m | m delete].

(SystemWindow windowsIn: cur 
      satisfying: [:w | w model canDiscardEdits])
   do: [:w | w delete].

Smalltalk garbageCollect.

SmalltalkImage current snapshot: true andQuit: true.


/opt/src/Squeak5.2-18229-64bit-201810190412-Linux/bin/squeak -headless nbody.squeak_run.image Include/squeak/make.st nbody.squeak 2&gt;/dev/null
cat Include/squeak/run.st
BenchmarksGame program!
SmalltalkImage current snapshot: false andQuit: true!



5.27s to complete and log all make actions

COMMAND LINE:
/opt/src/Squeak5.2-18229-64bit-201810190412-Linux/bin/squeak -headless nbody.squeak_run.image Include/squeak/run.st 50000000

PROGRAM OUTPUT:
-0.169075164
-0.169059907

pthread_setschedparam failed: Operation not permitted
This VM uses a separate heartbeat thread to update its internal clock
and handle events.  For best operation, this thread should run at a
higher priority, however the VM was unable to change the priority.  The
effect is that heavily loaded systems may experience some latency
issues.  If this occurs, please create the appropriate configuration
file in /etc/security/limits.d/ as shown below:

cat &lt;&lt;END | sudo tee /etc/security/limits.d/squeak.conf
*      hard    rtprio  2
*      soft    rtprio  2
END

and report to the squeak mailing list whether this improves behaviour.

You will need to log out and log back in for the limits to take effect.
For more information please see
https://github.com/OpenSmalltalk/opensmalltalk-vm/releases/tag/r3732#linux
    </pre>
  </section>
</article>
<footer>
  <nav>
    <ul>
      <li><a href="../license.html"><span>3-Clause BSD License</span></a>
    </ul>
  </nav>
</footer>

